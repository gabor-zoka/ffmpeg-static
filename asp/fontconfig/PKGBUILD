# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=fontconfig
pkgname=(fontconfig) 
pkgver=2.13.91+48+gfcb0420
pkgrel=2
epoch=2
pkgdesc="Library for configuring and customizing font access"
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
arch=(x86_64)
license=(custom)
depends=(expat freetype2)
makedepends=(git gperf)
checkdepends=(unzip)
_commit=fcb042028126d79ea5a5fa015b2b034b98656e73  # master
source=("git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#commit=$_commit"
        fontconfig.hook)
sha256sums=('SKIP'
            '8883f7e6e9d574ed52b89256507a6224507925715ddc85b3dfab9683df2f1e25')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

pkgver() {
  cd fontconfig
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd fontconfig
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd fontconfig
  ./configure --prefix=/usr \
    --disable-docs \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --localstatedir=/var \
    --disable-shared \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/local/share/fonts
  make
}

check() {
  cd fontconfig
  make -k check
}

package_fontconfig() {
  depends=(expat libfreetype.a)
  provides=(libfontconfig.a)
  install=fontconfig.install
  backup=(etc/fonts/fonts.conf)

  cd fontconfig
  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 ../*.hook
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING

  perl -i -p -e 's{-lfontconfig}{-lfontconfig -pthread}' "$pkgdir/usr/lib/pkgconfig/fontconfig.pc"
}

# vim:set sw=2 et:
