# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg
pkgver=4.2.4
pkgrel=1
epoch=2
pkgdesc='Complete solution to record, convert and stream audio and video'
arch=(x86_64)
url=https://ffmpeg.org/
license=(GPL3)
# As static build, everything must be makedepends.
makedepends=(
  nasm
  libvorbis
  bzip2
  xz
  zlib
  x265
  x264
  lame
)
source=(
  FFmpeg-n$pkgver.tar.gz::https://github.com/FFmpeg/FFmpeg/archive/n$pkgver.tar.gz
  vmaf-model-path.patch
)
sha256sums=('42f3d391dbf07b65a52d3d9eed8038ecd9fae53cf4e0e44e2adb95d0cd433b53'
            '8dff51f84a5f7460f8893f0514812f5d2bd668c3276ef7ab7713c99b71d7bd8d')

prepare() {
  cd FFmpeg-n$pkgver

  patch -Np1 -i "${srcdir}"/vmaf-model-path.patch
}

build() {
  cd FFmpeg-n$pkgver

  ./configure \
    --prefix=/usr \
    --cpu=host \
    --pkg-config-flags=--static \
    --extra-ldflags=-static \
    --enable-gray \
    --disable-debug \
    --enable-gpl \
    --disable-ffplay \
    --disable-doc \
    --disable-encoder=vorbis \
    --enable-libvorbis \
    --enable-libx265 \
    --enable-libx264 \
    --enable-libmp3lame

  make
}

package() {
  make DESTDIR="${pkgdir}" -C FFmpeg-n$pkgver install
}

# vim: ts=2 sw=2 et:
